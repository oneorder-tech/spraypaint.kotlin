apply plugin: "maven-publish"
apply plugin: "signing"

task javadocsJar(type: Jar, dependsOn: "dokkaJavadoc") {
    getArchiveClassifier().set("javadoc")
    from dokkaJavadoc.outputDirectory
}

task sourcesJar(type: Jar) {
    getArchiveClassifier().set("sources")
    from sourceSets.main.allJava
}

afterEvaluate {
    publishing {
        def properties = project.ext.mavenPublishProperties

        repositories {
            maven {
                name "SonatypeMavenCentral"
                url properties.repository.url
                credentials {
                    username properties.repository.username
                    password properties.repository.password
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.java

                groupId = properties.group
                version = properties.version
                artifactId = properties.artifactId

                artifact sourcesJar
                artifact javadocsJar

                pom {
                    name = properties.name
                    description = properties.description
                    url = properties.url
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'Michaelibrahim93'
                            name = 'Michael Ibrahim'
                            email = 'michael.ibrahim@oneorder.net'
                        }
                    }
                    scm {
                        connection.set("https://github.com/oneorder-tech/spraypaint.kotlin")
                        developerConnection.set("scm:git@github.com:oneorder-tech/spraypaint.kotlin.git")
                        url.set("scm:git@github.com:oneorder-tech/spraypaint.kotlin.git")
                    }
                }
                signing {
                    sign publishing.publications.release
                    sign configurations.archives
                }
            }
        }
    }
}