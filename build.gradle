buildscript {
    ext.kotlin_version = '1.3.41'

    ext.bintrayPublish = [
            userOrg       : 'undabot',
            groupId       : 'com.undabot',
            repoName      : 'izzy-json-api-parser',
            publishVersion: '0.0.1',
            githubLink    : 'https://github.com/undabot/izzy-json-api-android',
            key           : System.getenv("BINTRAY_KEY"),
            user          : System.getenv("BINTRAY_USER")
    ]

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.novoda:bintray-release:0.9'
        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.4.1"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.16.0"
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply from: '../dependencies.gradle'
    apply from: rootProject.file('quality/ktlint.gradle')
    apply from: rootProject.file('quality/detekt.gradle')
    group = 'com.github.undabot'

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            // Set JVM target to 1.8
            jvmTarget = "1.8"
        }
    }
}

wrapper {
    gradleVersion = '6.8'
    distributionUrl = distributionUrl.replace("bin", "all")
}